(ns isaac.config.cli.schema
  "isaac config schema — print the schema for a schema path."
  (:require
    [c3kit.apron.schema.path :as schema-path]
    [clojure.string :as str]
    [isaac.config.cli.common :as common]
    [isaac.config.cli.inspect :as inspect]
    [isaac.config.comm-kinds :as comm-kinds]
    [isaac.config.schema.term :as schema-term]
    [isaac.module.loader :as module-loader]))

(def option-spec
  (into [[nil  "--tree" "Expand every named sub-schema as its own section"]]
        inspect/structured-option-spec))

(def ^:private examples
  (str "  isaac config schema\n"
        "  isaac config schema crew\n"
        "  isaac config schema providers.value\n"
        "  isaac config schema crew.value.model\n"
        "  isaac config schema providers.value.api-key\n"
        "  isaac config schema --tree"))

(defn help []
  (common/render-help
    {:command     "isaac config schema"
     :params      "[schema-path] [options]"
     :description (str "Print the config schema for a schema path. Schema paths use literal\n"
                       "'key' and 'value' segments to address the key/value types of a map —\n"
                       "for example 'crew.value' is the schema of a single crew entry,\n"
                       "'crew.value.soul' drills into the soul field on that entry.")
     :option-spec option-spec
     :examples    examples}))

(defn- guidance []
  (str "\nTry:\n" examples))

(defn- schema-context [opts]
  (common/schema-context opts))

(defn- comm-resolver [module-index]
  (let [module-index (or module-index (module-loader/builtin-index))]
    (if module-index
      #(comm-kinds/comm-kinds module-index)
      comm-kinds/comm-kinds)))

(def ^:private collection-surfaces #{"comms" "providers"})

(defn- substituted-path
  "When `path-str` targets a map-of surface via a literal slot-id segment
   followed by further drilling (e.g. `comms.discord.token`), rewrite the
   slot segment as `.value` so apron's standard walker descends into the
   value-spec. Requires at least three segments so a two-segment typo
   (e.g. `providers.valued`) is not silently rewritten to `providers.value`.
   Returns nil when no substitution applies."
  [path-str]
  (let [segments (some-> path-str (str/split #"\."))]
    (when (and (<= 3 (count segments))
               (contains? collection-surfaces (first segments))
               (not (#{"value" "key"} (second segments))))
      (str/join "." (cons (first segments) (cons "value" (drop 2 segments)))))))

(defn- resolve-path [root path-str]
  (if (str/blank? path-str)
    root
    (try
      (or (schema-path/schema-at root path-str)
          (some-> path-str substituted-path (->> (schema-path/schema-at root))))
      (catch Exception _ nil))))

(defn- print-schema! [opts path-str options]
  (if-let [format-error (inspect/structured-format-conflict? options)]
    format-error
    (let [{:keys [config module-index root]} (schema-context opts)
          spec     (resolve-path root path-str)
          {:keys [tree edn json]} options]
      (if spec
        (cond
          (or edn json)
          (do (inspect/print-structured! edn json spec) 0)

          :else
          (let [root?  (or (nil? path-str) (str/blank? path-str))
                output (schema-term/spec->term spec {:color?            (common/stdout-tty?)
                                                     :config            config
                                                     :module-index      module-index
                                                     :path-prefix       (common/path-prefix path-str)
                                                     :deep?             (boolean tree)
                                                     :width             80
                                                     :options-resolvers {:comms (comm-resolver module-index)}})]
            (if output
              (do
                (println (if root? (str output (guidance)) output))
                0)
              (do
                (binding [*out* *err*]
                  (println (str "Path not found in config schema: " path-str)))
                1))))
        (do
          (binding [*out* *err*]
            (println (str "Path not found in config schema: " path-str)))
          1)))))

(defn run [opts arguments options]
  (print-schema! opts (common/normalize-path (first arguments)) options))

(def subcommand
  {:option-spec option-spec
   :runner      run
   :help-text   help})
